<?xml version='1.0' encoding='utf-8' ?>
<!--
Copyright (c) 2009, Yoav Aviram
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions
are met:

* Redistributions of source code must retain the above copyright
notice, this list of conditions and the following disclaimer.
* Redistributions in binary form must reproduce the above copyright
notice, this list of conditions and the following disclaimer in
the documentation and/or other materials provided with the distribution.
* Neither the name of the author nor the names of its
contributors may be used to endorse or promote products derived
from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
POSSIBILITY OF SUCH DAMAGE.
-->
<xsl:stylesheet version="1.0" 
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
	xmlns:atom="http://www.w3.org/2005/Atom">
	<xsl:output method="xml" version="1.0" encoding="UTF-8" omit-xml-declaration="yes" indent="no" />
	
	<xsl:template match="/">
		<xsl:apply-templates select="atom:feed" />
	</xsl:template>
	
	<xsl:template match="atom:feed">
		<feed format="Atom" version="1.0" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:noNamespaceSchemaLocation="feed_xsd.txt">
			<xsl:apply-templates select="atom:title" />
			<xsl:apply-templates select="atom:link" />
			<xsl:apply-templates select="atom:tagline" />
			<xsl:apply-templates select="xml:lang" />
			<xsl:apply-templates select="atom:copyright" />
			<xsl:apply-templates select="atom:author" />
            <categories>
                <xsl:apply-templates select="atom:category" />
            </categories>
			<xsl:apply-templates select="atom:entry" />
		</feed>
	</xsl:template>
	
	<xsl:template match="atom:title">
		<title>
			<xsl:value-of select="." />
		</title>
	</xsl:template>
	
	<xsl:template match="atom:link">
		<url>
			<xsl:value-of select="@href" />
		</url>
	</xsl:template>
	
	<xsl:template match="atom:tagline">
		<description>
			<xsl:value-of select="." />
		</description>
	</xsl:template>
	
	<xsl:template match="xml:lang">
		<language>
			<xsl:value-of select="." />
		</language>
	</xsl:template>
	
	<xsl:template match="atom:copyright">
		<copyright>
			<xsl:value-of select="." />
		</copyright>
	</xsl:template>
	
	<xsl:template match="atom:author">
		<authorName>
			<xsl:value-of select="atom:name" />
		</authorName>
		<authorEmail>
			<xsl:value-of select="atom:email" />
		</authorEmail>
	</xsl:template>
	
	<xsl:template match="atom:id">
		<entryUniqueId>
			<xsl:value-of select="." />
		</entryUniqueId>
	</xsl:template>
	
	<xsl:template match="atom:issued | atom:published">
		<publishDateTime>
			<xsl:variable name="DateTime" select="." />
			<xsl:variable name="day" select="substring-before($DateTime,'T')" />
			<xsl:variable name="time-temp" select="substring-after($DateTime,'T')" />
			<xsl:variable name="time" select="substring($time-temp,0,8)" />
			<xsl:value-of select="$day" />
			<xsl:value-of select="' '" />
			<xsl:value-of select="$time" />
			<xsl:value-of select="' +0000'" />
		</publishDateTime>
	</xsl:template>
	
	<xsl:template match="atom:modified | atom:updated">
		<lastUpdatedDateTime>
			<xsl:variable name="DateTime" select="." />
			<xsl:variable name="day" select="substring-before($DateTime,'T')" />
			<xsl:variable name="time-temp" select="substring-after($DateTime,'T')" />
			<xsl:variable name="time" select="substring($time-temp,0,9)" />
			<xsl:value-of select="$day" />
			<xsl:value-of select="' '" />
			<xsl:value-of select="$time" />
			<xsl:value-of select="' +0000'" />
		</lastUpdatedDateTime>
	</xsl:template>

	<xsl:template match="atom:content">
		<xsl:copy-of select="." />
	</xsl:template>
	
	<xsl:template match="atom:summary">
		<description>
			<xsl:value-of select="." />
		</description>
	</xsl:template>

    <xsl:template match="atom:category">
        <category>
            <xsl:value-of select="@term"/>
        </category>
    </xsl:template>

	<xsl:template match="atom:entry">
		<entry>
			<xsl:apply-templates select="atom:id" />
			<xsl:apply-templates select="atom:issued | atom:published" />
			<xsl:apply-templates select="atom:modified | atom:updated" />
			<xsl:apply-templates select="atom:title" />
			<xsl:apply-templates select="atom:link" />
			<xsl:apply-templates select="atom:author" />
            <categories>
                <xsl:apply-templates select="atom:category" />
            </categories>
			<xsl:apply-templates select="atom:summary" />
			<xsl:apply-templates select="atom:content" />
		</entry>
	</xsl:template>

</xsl:stylesheet>
